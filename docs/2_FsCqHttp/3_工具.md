---
title: 工具
category: FsCqHttp
categoryindex: 2
index: 3
---

## Utils.TextResponse
文本输出工具类。

### 提供功能
1. 进行简单的文本格式化。
2. 根据输入的ResponseType，选择合适的输出方式。
3. 实现IDisposable，可以使用use绑定省去手动Flush()。
4. 在CqMessageEventArgs及其衍生类中可以使用OpenResponse方法直接调用。

### 相关配置

- NewLine 换行符
- TextLengthLimit 文本输出下最长文字量
- ImageIgnoreSendCheck 是否进行CanSendImage API检查。酷Q之后的平台基本都支持发图，可以设置true节约时间。
- ImageOutputFont 图片输出时用的字体。因为TextTable排版方式限制，请务必使用等宽字体。
- ImageOutputSize 图片输出时用的字号。
- ImageTextColor 图片输出时文字颜色。
- ImageRowColorA 图片输出时交错行配色。
- ImageRowColorB 图片输出时交错行配色。

### 注意
1. 在使用use语句的情况下，请使用TextResponse.Abort方法中断输出。
2. 已知在Linux环境中对颜文字使用MeasureByGraphic会造成libgdiplus异常，此后所有测量均返回0。


## Utils.TextTable
使用TextResponse实现简单的文本表格排版。

### 相关配置
- TableCellPadding 空白单元格的默认值，一般用```--```
- TableGraphicMeasure 字符串宽度计算方法。true使用System.Drawing测量，依赖libgdiplus。false则使用正则猜测宽度，不准确而且性能低。

## Utils.AliasMapper
因为Bot指令处理的时候常常会遇到以下情况：

1. 需要大小写不敏感（尤其手机用户）。
2. 文本存在简写、别称和全称等情况，需要映射到一个统一值。

所以提供本工具类简单处理映射问题。

## Utils.UserOption

### 使用
1. 根据需求继承OptionBase或者CommandOption（默认提供一个```text```选项指示指令输出方式）
2. 在类内定义相应的OptionCell
    * ```OptionCell``` 不提供值，使用IsDefined检查是否被定义。
    * ```OptionCell<'T>``` 可以通过```Value```或```Values```访问一个或者一组值。需要自己重写```ConvertValue```方法。
    * ```OptionCellSimple<'T>``` 简化的```OptionCell<'T>```，使用```Convert.ChangeType```进行简单转换，适合.NET自带的简单类型。
    * 可以通过```Aliases```属性指定选项的别名。
    * 可以通过```ArgIndex```属性从指定位置读取值（用于子指令处理）。
    * 如果使用时只关心值，则let绑定后用属性返回对应值。
    * 如果关心选项的状态，可以使用member val绑定选项。
3. OptionBase().Parse()后使用对应的成员。

常规使用：
```fsharp
namespace KPX.TheBot.Module.EveModule.Utils.Config

type EveConfigParser() as x = 
    inherit CommandOption()

    let ime = OptionCellSimple(x, "ime", 2)
    let dme = OptionCellSimple(x, "dme", 10)
    let sci = OptionCellSimple(x, "sci", 4)
    let tax = OptionCellSimple(x, "tax", 10)

    let p = OptionCell(x, "p")
    let r = OptionCell(x, "r")
    let buy = OptionCell(x, "buy")

    member x.SetDefaultInputMe(value)= ime.Default <- value

    member x.InputMe = ime.Value

    member x.DerivationMe = dme.Value

    member x.SystemCostIndex = sci.Value

    member x.StructureTax = tax.Value

    member x.ExpandReaction = r.IsDefined

    member x.ExpandPlanet = p.IsDefined

    member x.MaterialPriceMode =
        if buy.IsDefined then PriceFetchMode.BuyWithTax else PriceFetchMode.Sell
    
    /// 自动获取输入材料效率
    /// 如果ime被设置，返回ime
    /// 如果为设置，根据蓝图metaGroupId返回默认效率
    member x.GetImeAuto(item : EveType) =
        if ime.IsDefined then
            x.InputMe
        else
            match item.MetaGroupId with
            | 1
            | 54 -> 10 // T1装备建筑默认10
            | 2
            | 14
            | 53 -> 2 // T2/T3装备 建筑默认2
            | _ -> 0 // 其他默认0
    
    interface KPX.TheBot.Data.EveData.Process.IEveCalculatorConfig with
        member x.InputMe = x.InputMe
        member x.DerivationMe = x.DerivationMe
        member x.ExpandPlanet = x.ExpandPlanet
        member x.ExpandReaction = x.ExpandReaction
```

有关绑定状态：
```fsharp
module KPX.TheBot.Module.XivModule.Utils.CommandUtils

type XivWorldOpt(cb : CommandOption, key, defVal) =
    inherit OptionCell<World>(cb, key, defVal)

    override x.ConvertValue(name) = World.GetWorldByName(name)

type XivOption() as x =
    // FF14有很多带有冒号的道具名，添加到AsNonOption避免报错丢失。
    inherit CommandOption(UndefinedOptionHandling = UndefinedOptionHandling.AsNonOption)

    static let defaultServer = World.GetWorldByName("拉诺西亚")

    // 因为下游可能需要第一个也可能需要全部，所以保留显式的Cell
    member val World = XivWorldOpt(x, "world", defaultServer)
    
// 也可以写成这样，看抽象要求
type XivOptionSimple() as x =
    // FF14有很多带有冒号的道具名，添加到AsNonOption避免报错丢失。
    inherit CommandOption(UndefinedOptionHandling = UndefinedOptionHandling.AsNonOption)

    static let defaultServer = World.GetWorldByName("拉诺西亚")
    let worldOpt = XivWorldOpt(x, "world", defaultServer)
    
    member x.World = worldOpt.Value
    member x.Worlds = worldOpt.Value

```


### 备注
本来想实现类似Argu的方案，但是后来想了想问题比较多就放弃了。

比如Argu使用DU的方式去定义选项，但是这样默认值必须留到使用的时候定义。

考虑到实际使用中选项经常会在多个地方用（比如EVE相关的模块），每次用都要自己设定默认值不利于维护。

所以还是使用这种看起来比较丑的思路解决了。

## Utils.Subcommands
使用DU定义子指令及其相关处理，类似Argu方案。

利用DU的特性可以利用编译器检查和pattern matching省很多事。

示例，实际代码以后可能会改：
```fsharp
namespace KPX.TheBot.Module.TRpgModule.StCommand

type StShowCommandOpts() as x =
    inherit OptionBase()

    let skillName =
        OptionCellSimple(x, "skill", "", ArgIndex = Some 0)

    member x.SkillName =
        if skillName.IsDefined then
            let pn = skillName.Value
            let succ, value = SkillNameAlias.TryGetValue(pn)
            if succ then Some value else Some pn
        else
            None

type PcSubcommands =
    | New of chrName : string
    | List
    | [<AltCommandName("rm")>] Remove of name : string
    | [<AltCommandName("clr")>] Clear
    | Get
    | Show of StShowCommandOpts
    | Lock
    | Unlock
    | Rename of newName : string
    | Set of name : string
    | [<AltCommandName("cp")>] Copy of fromName : string

    interface ISubcommandTemplate with
        member x.Usage =
            match x with
            | New _ -> "创建空白卡"
            | List -> "看角色列表"
            | Remove _ -> "删除角色 remove 角色名称"
            | Clear -> "清空数据"
            | Get -> "获取当前角色属性"
            | Show _ -> "展示角色属性 show/show 属性名称"
            | Lock -> "锁定当前角色"
            | Unlock -> "解锁当前角色"
            | Rename _ -> "角色改名 rename 新名字"
            | Copy _ -> "角色复制 copy 角色名 @接收方"
            | Set _ -> "设置当前角色 set 角色名"
            
            
// 在使用时，逻辑代码已省略：
[<CommandHandlerMethod(".pc", "角色操作，不带参数查看帮助", "")>]
member x.HandlePC(cmdArg : CommandEventArgs) =
    match SubcommandParser.Parse<PcSubcommands>(cmdArg.HeaderArgs) with
    | None ->
        // 没匹配就显示帮助
        using
            (cmdArg.OpenResponse(ForceText))
            (fun ret ->
                let help =
                    SubcommandParser.GenerateHelp<PcSubcommands>()
                 for line in help do
                    ret.WriteLine(line))
    | Some (New chrName) -> ()
    | Some List -> ()
    | Some (Remove name) -> ()
    | Some Clear -> ()
    | Some Get -> ()
    | Some (Show opt) -> ()
    | Some Lock -> ()
    | Some Unlock -> ()
    | Some (Rename name) -> ()
    | Some (Copy name) -> ()
    | Some (Set name) -> ()
```